plugins {
  id 'maven-publish'
  id 'net.fabricmc.fabric-loom' version '1.15-SNAPSHOT' apply false
  id 'net.fabricmc.fabric-loom-remap' version '1.15-SNAPSHOT' apply false

  // https://github.com/ReplayMod/preprocessor
  // https://github.com/Fallen-Breath/preprocessor
  id 'com.replaymod.preprocess' version 'c5abb4fb12'
}

preprocess {
    strictExtraMappings = false

    // def mc114 = createNode('1.14.4' , 1_14_04, '')
    // def mc115 = createNode('1.15.2' , 1_15_02, '')
    // def mc116 = createNode('1.16.5' , 1_16_05, '')
    def mc1171 = createNode('1.17.1' , 1_17_01, '')
    def mc1182 = createNode('1.18.2' , 1_18_02, '')
    def mc1192 = createNode('1.19.2' , 1_19_02, '')
    def mc1193 = createNode('1.19.3' , 1_19_03, '')
    def mc1194 = createNode('1.19.4' , 1_19_04, '')
    def mc1201 = createNode('1.20.1' , 1_20_01, '')
    def mc1202 = createNode('1.20.2' , 1_20_02, '')
    def mc1211 = createNode('1.21.1' , 1_21_01, '')
    def mc1214 = createNode('1.21.4' , 1_21_04, '')
    def mc1215 = createNode('1.21.5' , 1_21_05, '')
    def mc1218 = createNode('1.21.8' , 1_21_08, '')
    def mc12110 = createNode('1.21.10' , 1_21_10, '')
    def mc12111 = createNode('1.21.11' , 1_21_11, '')
    // def mcSnapshot = createNode('snapshot', 1_19_00, '')

  // mc115.link(mc114, null)
  // mc116.link(mc115, null)
  // mc117.link(mc116, null)

  mc1171.link(mc1182, null)
  mc1182.link(mc1192, file('versions/mapping-1.18.2-1.19.2.txt'))
  mc1192.link(mc1193, null)
  mc1193.link(mc1194, null)
  mc1194.link(mc1201, null)
  mc1201.link(mc1202, null)
  mc1202.link(mc1211, null)
  mc1211.link(mc1214, null)
  mc1214.link(mc1215, null)
  mc1215.link(mc1218, null)
  mc1218.link(mc12110, null)
  mc12110.link(mc12111, null)
  // mc119.link(mcSnapshot, null)

  for (final def node in getNodes()) {
    findProject(node.project).ext.mcVersion = node.mcVersion
  }
}

tasks.register('buildAndGather') {
  subprojects {
    dependsOn project.tasks.named('build').get()
  }
  doFirst {
    println 'Gathering builds'
    def buildLibs = {
      p -> p.buildDir.toPath().resolve('libs')
    }
    delete fileTree(buildLibs(rootProject)) {
      include '*'
    }
    subprojects {
      copy {
        from(buildLibs(project)) {
          include '*.jar'
          exclude '*-dev.jar', '*-sources.jar', '*-shadow.jar'
        }
        into buildLibs(rootProject)
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
      }
    }
  }
}
